group 'com.github.pgutkowski'
version '0.2.3-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.3'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: "me.champeau.gradle.jmh"

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.16'

    compile "com.fasterxml.jackson.core:jackson-databind:2.8.7"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7"

    compile "com.github.ben-manes.caffeine:caffeine:1.0.0"

    testCompile "org.jetbrains.kotlinx:kotlinx-html-jvm:0.6.3"
    testCompile "io.netty:netty-all:4.1.9.Final"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.hamcrest:hamcrest-all:1.3"
}

jmh {
    jmhVersion = '1.19'
    duplicateClassesStrategy = 'warn'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

coveralls {
    sourceDirs += ['src/main/kotlin']
}

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    pkg {
        repo = 'Maven'
        name = 'KGraphQL'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/pgutkowski/KGraphQL.git'
        publications = ['MyPublication']

        version {
            name = '0.2.3'
            desc = 'KGraphQL alpha release'
            released  = new Date()
            vcsTag = '0.2.3'
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0' //version required
}

